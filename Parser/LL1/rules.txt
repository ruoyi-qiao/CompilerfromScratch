<program> ::= <compoundstmt>

<stmt> ::=  <ifstmt> 
            | <whilestmt> 
            | <assgstmt> 
            | <compoundstmt>

<compoundstmt> ::= "{" <stmts> "}"

<stmts> ::= <stmt> <stmts> 
            | ""

<ifstmt> ::= "if" "(" <boolexpr> ")" "then" <stmt> "else" <stmt>

<whilestmt> ::= "while" "(" <boolexpr> ")" <stmt>

<assgstmt> ::= ID "=" <arithexpr> ";"

<boolexpr> ::= <arithexpr> <boolop> <arithexpr>

<boolop> ::= "<" 
            | ">" 
            | "<=" 
            | ">=" 
            | "=="

<arithexpr> ::= <multexpr> <arithexprprime>

<arithexprprime> ::= "+" <multexpr> <arithexprprime> 
            | "-" <multexpr> <arithexprprime> 
            | ""

<multexpr> ::= <simpleexpr> <multexprprime>

<multexprprime> ::= "*" <simpleexpr> <multexprprime> 
            | "/" <simpleexpr> <multexprprime> 
            | ""

<simpleexpr> ::= ID 
                | NUM 
                | "(" <arithexpr> ")"
